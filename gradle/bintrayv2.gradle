apply plugin: 'com.jfrog.bintray'

version = libraryVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
bintray {
    user = bintray_user
    key = bintray_apikey

    configurations = ['archives']

    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses

        publish = false

        publicDownloadNumbers = true

        version {
            desc = libraryDescription
            // https://www.jfrog.com/confluence/display/BT/Managing+Uploading+Content+New+UI#ManagingUploadingContentNewUI-GPGSigning
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false

                //Optional. The passphrase for GPG signing'
                passphrase = gpg_password
            }
        }
    }
}